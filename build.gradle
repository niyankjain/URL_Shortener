plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.io.infracloud'
version = '1.0.0'
description = 'URL Shortener Service for Infra cloud'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = 'plain'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    // Exclude configuration files from the JAR
    exclude 'application*.yaml'
    exclude 'logback.xml'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

tasks.register('copyDependencies', Copy) {
    from configurations.runtimeClasspath
    into layout.buildDirectory.dir("libs/lib").get().asFile
}

tasks.register('copyConfig', Copy) {
    from 'src/main/resources'
    into layout.buildDirectory.dir("libs/config").get().asFile
    include 'application*.yaml'
    include 'logback.xml'
    include 'run.sh'
}

// Make sure config is copied before building
tasks.named('build') {
    dependsOn 'copyDependencies'
    dependsOn 'copyConfig'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-h2console'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation("org.apache.commons:commons-lang3:3.20.0")
    implementation("org.hashids:hashids:1.0.3")
    implementation("net.bull.javamelody:javamelody-spring-boot4-starter:2.6.0")
    implementation('net.logstash.logback:logstash-logback-encoder:7.4')
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-actuator-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}
